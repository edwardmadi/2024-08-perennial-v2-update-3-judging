Jolly Mauve Parakeet

Medium

# settle's keepers will not receive their fees

### Summary

settle's keepers will not receive their fees due to keeper oracle sending fees to the factory instead of factory's caller

### Root Cause

Whenever keepers  will try performs a list of local settlement callback, they will call settle
There is a description how that function should pay keeper incentive
>  Performs a list of local settlement callbacks. Pays out a keeper incentive if all supplied local settlement callbacks succeed

```solidity
    /// @notice Performs a list of local settlement callbacks
    /// @dev Pays out a keeper incentive if all supplied local settlement callbacks succeed
    function settle(bytes32[] memory oracleIds, uint256[] memory versions, uint256[] memory maxCounts) external {
        if (oracleIds.length == 0 || oracleIds.length != versions.length || oracleIds.length != maxCounts.length)
            revert KeeperFactoryInvalidSettleError();

        for (uint256 i; i < oracleIds.length; i++)
            IKeeperOracle(address(oracles[oracleIds[i]])).settle(versions[i], maxCounts[i]);
    }
```
[contracts/keeper/KeeperFactory.sol#L168](https://github.com/sherlock-audit/2024-08-perennial-v2-update-3/blob/ee9f86708b84aea72cb2eb2578082bf8eaf8cacc/perennial-v2/packages/perennial-oracle/contracts/keeper/KeeperFactory.sol#L168)
It will call settle inside keeper oracle which will send fee to factory instead of factory caller
```solidity
    function settle(uint256 version, uint256 maxCount) external onlyFactory {
        EnumerableSet.AddressSet storage callbacks = _localCallbacks[version];

        if (_global.latestVersion < version) revert KeeperOracleVersionOutsideRangeError();
        if (maxCount == 0) revert KeeperOracleInvalidCallbackError();
        if (callbacks.length() == 0) revert KeeperOracleInvalidCallbackError();

        IMarket market = oracle.market();

        for (uint256 i; i < maxCount && callbacks.length() > 0; i++) {
            address account = callbacks.at(0);
            market.settle(account);
            callbacks.remove(account);
            emit CallbackFulfilled(SettlementCallback(market, account, version));

            // full settlement fee already cleamed in commit
            PriceResponse memory priceResponse = _responses[version].read();
            market.token().push(msg.sender, UFixed18Lib.from(priceResponse.asyncFee)); // @audit send to factory instead of factory caller
        }
    }

```
[contracts/keeper/KeeperOracle.sol#L174](https://github.com/sherlock-audit/2024-08-perennial-v2-update-3/blob/ee9f86708b84aea72cb2eb2578082bf8eaf8cacc/perennial-v2/packages/perennial-oracle/contracts/keeper/KeeperOracle.sol#L174)

### Internal pre-conditions

None

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Keepers will not receive their reward which will lead to not performing settling in time and not rewarding keepers

### PoC

_No response_

### Mitigation

```diff
-    function settle(uint256 version, uint256 maxCount) external onlyFactory {
+    function settle(uint256 version, uint256 maxCount, address receiver) external onlyFactory {
        EnumerableSet.AddressSet storage callbacks = _localCallbacks[version];

        if (_global.latestVersion < version) revert KeeperOracleVersionOutsideRangeError();
        if (maxCount == 0) revert KeeperOracleInvalidCallbackError();
        if (callbacks.length() == 0) revert KeeperOracleInvalidCallbackError();

        IMarket market = oracle.market();

        for (uint256 i; i < maxCount && callbacks.length() > 0; i++) {
            address account = callbacks.at(0);
            market.settle(account);
            callbacks.remove(account);
            emit CallbackFulfilled(SettlementCallback(market, account, version));

            // full settlement fee already cleamed in commit
            PriceResponse memory priceResponse = _responses[version].read();
-            market.token().push(msg.sender, UFixed18Lib.from(priceResponse.asyncFee));
+            market.token().push(receiver, UFixed18Lib.from(priceResponse.asyncFee));
        }
    }
```

```diff
    function settle(bytes32[] memory oracleIds, uint256[] memory versions, uint256[] memory maxCounts) external {
        if (oracleIds.length == 0 || oracleIds.length != versions.length || oracleIds.length != maxCounts.length)
            revert KeeperFactoryInvalidSettleError();

        for (uint256 i; i < oracleIds.length; i++)
-            IKeeperOracle(address(oracles[oracleIds[i]])).settle(versions[i], maxCounts[i]);
+            IKeeperOracle(address(oracles[oracleIds[i]])).settle(versions[i], maxCounts[i], msg.sender);
        
    }
```