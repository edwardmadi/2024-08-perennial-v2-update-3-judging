High Neon Llama

Medium

# The `Market.migrate()` function has no effect and does not migrate `PositionStorageGlobal` to the new storage layout, breaking the migration assumption.

## Summary

One of the key requirements during the migration from version 2.2 to 2.3 is to consolidate global and local position storage layouts from 2 slots to 1 slot due to refactoring in v2.2, described [here](https://github.com/equilibria-xyz/perennial-v2/blob/edcf4d1a8ddd01f979118d2a9e7bfab3fd6f6c75/runbooks/MIGRATION_v2.3.md?plain=1#L56-L60) and [here](https://github.com/equilibria-xyz/perennial-v2/pull/424). This consolidation for global positions needs to be done on a market-by-market basis by calling the `migrate()` function.

The issue is that the `migrate()` function does not perform as expected, and after migration, `PositionStorageGlobal` continues to use the old 2-slot storage layout. It still reads the `maker` value from slot 1, and the `PositionStorageGlobalLib.migrate()` function is ineffective because the `read()` and `store()` functions were never updated to use the new 1-slot storage layout.

## Vulnerability Detail

The new storage layout should appear as follows:

```solidity
File: Position.sol
307: ///     struct StoredPositionGlobal {
308: ///         /* slot 0 */
309: ///         uint32 timestamp;
310: ///         uint32 __unallocated__;
311: ///         uint64 maker;
312: ///         uint64 long;
313: ///         uint64 short;
314: ///
315: ///         /* slot 1 */
316: ///         uint64 maker (deprecated);
317: ///         uint192 __unallocated__;
318: ///     }
```

In `PositionStorageGlobalLib.migrate()`, the intended steps are:

1. Read the position, including the new `maker` value from slot 0 via the updated `read()` function.
2. Read the old `maker` value from slot 1 of the `StoredPositionGlobal` version 2.2 storage layout.
3. Ensure that no previous migration has occurred.
4. Transfer the old `maker` value from slot 1 to the new `maker` slot in slot 0.

```solidity
File: Position.sol
351:     function migrate(PositionStorageGlobal storage self) external {
352:@>       Position memory position = read(self);
353:         uint256 slot1 = self.slot1;
354:@>       UFixed6 deprecatedMaker = UFixed6.wrap(uint256(slot1 << (256 - 64)) >> (256 - 64));
355: 
356:         // only migrate if the deprecated maker is set and new maker is unset to avoid double-migration
357:         if (deprecatedMaker.isZero() || !position.maker.isZero())
358:             revert PositionStorageLib.PositionStorageInvalidMigrationError();
359: 
360:         position.maker = deprecatedMaker;
361:@>       store(self, position);
362:     }
```

However, these steps are not working as expected because the `read()` and `store()` functions were not updated to reflect the new storage layout. While this does not immediately impact protocol functionality, it means that one of the critical migration steps described in the [migration guide](https://github.com/equilibria-xyz/perennial-v2/blob/edcf4d1a8ddd01f979118d2a9e7bfab3fd6f6c75/runbooks/MIGRATION_v2.3.md) will not be executed.

This issue could lead to future errors, where the deprecated `maker` from slot 1 may be incorrectly assumed to be removable, which would render the protocol unusable.

## Impact

- Migration from version 2.2 to 2.3 will not occur as expected.
- Future versions may break the protocol due to incorrect assumptions about storage layout.

## Code Snippet

https://github.com/sherlock-audit/2024-08-perennial-v2-update-3/blob/main/perennial-v2/packages/perennial/contracts/types/Position.sol#L307-L318  
https://github.com/sherlock-audit/2024-08-perennial-v2-update-3/blob/main/perennial-v2/packages/perennial/contracts/types/Position.sol#L351-L362  
https://github.com/sherlock-audit/2024-08-perennial-v2-update-3/blob/main/perennial-v2/packages/perennial/contracts/types/Position.sol#L321-L329  
https://github.com/sherlock-audit/2024-08-perennial-v2-update-3/blob/main/perennial-v2/packages/perennial/contracts/types/Position.sol#L331-L349  

## Tool used

Manual Review

## Recommendation

Update the `read()` and `store()` functions in the `PositionStorageGlobalLib` library to reflect the new 1-slot storage layout.

### Updated `read()` function:

```diff
    function read(PositionStorageGlobal storage self) internal view returns (Position memory) {
        (uint256 slot0, uint256 slot1) = (self.slot0, self.slot1);
        return Position(
            uint256(slot0 << (256 - 32)) >> (256 - 32),
-           UFixed6.wrap(uint256(slot1 << (256 - 64)) >> (256 - 64)),
+           UFixed6.wrap(uint256(slot0 << (256 - 32 - 64)) >> (256 - 64)),
-           UFixed6.wrap(uint256(slot0 << (256 - 32 - 48 - 48 - 64)) >> (256 - 64)),
+           UFixed6.wrap(uint256(slot0 << (256 - 32 - 32 - 64 - 64)) >> (256 - 64)),
-           UFixed6.wrap(uint256(slot0 << (256 - 32 - 48 - 48 - 64 - 64)) >> (256 - 64))
+           UFixed6.wrap(uint256(slot0 << (256 - 32 - 32 - 64 - 64 - 64)) >> (256 - 64))
        );
    }
```

### Updated `store()` function:

```diff
    function store(PositionStorageGlobal storage self, Position memory newValue) public {
        PositionStorageLib.validate(newValue);

        if (newValue.maker.gt(UFixed6.wrap(type(uint64).max))) revert PositionStorageLib.PositionStorageInvalidError();
        if (newValue.long.gt(UFixed6.wrap(type(uint64).max))) revert PositionStorageLib.PositionStorageInvalidError();
        if (newValue.short.gt(UFixed6.wrap(type(uint64).max))) revert PositionStorageLib.PositionStorageInvalidError();

        uint256 encoded0 =
            uint256(newValue.timestamp << (256 - 32)) >> (256 - 32) |
+           uint256(UFixed6.unwrap(newValue.maker) << (256 - 64)) >> (256 - 32 - 32 - 64) |
-           uint256(UFixed6.unwrap(newValue.long) << (256 - 64)) >> (256 - 32 - 48 - 48 - 64) |
+           uint256(UFixed6.unwrap(newValue.long) << (256 - 64)) >> (256 - 32 - 32 - 64 - 64) |
-           uint256(UFixed6.unwrap(newValue.short) << (256 - 64)) >> (256 - 32 - 48 - 48 - 64 - 64);
+           uint256(UFixed6.unwrap(newValue.short) << (256 - 64)) >> (256 - 32 - 32 - 64 - 64 - 64);
-       uint256 encoded1 =
-           uint256(UFixed6.unwrap(newValue.maker) << (256 - 64)) >> (256 - 64);

        assembly {
            sstore(self.slot, encoded0)
-           sstore(add(self.slot, 1), encoded1) 
        }
    }
```

It is also advisable to clear the old `maker` in the `migrate()` function:

```diff
    function migrate(PositionStorageGlobal storage self) external {
        Position memory position = read(self);
        uint256 slot1 = self.slot1;
        UFixed6 deprecatedMaker = UFixed6.wrap(uint256(slot1 << (256 - 64)) >> (256 - 64));

        // only migrate if the deprecated maker is set and new maker is unset to avoid double-migration
        if (deprecatedMaker.isZero() || !position.maker.isZero())
            revert PositionStorageLib.PositionStorageInvalidMigrationError();

        position.maker = deprecatedMaker;
+
+       uint256 encoded1;
+       assembly {
+           sstore(add(self.slot, 1), encoded1) 
+       }
+
        store(self, position);
    }
```